#                                               -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.

AC_INIT([SysteMoC], [0.8], [joachim.falk@informatik.uni-erlangen.de christian.zebelein@informatik.uni-erlangen.de], [SysteMoC--mainline--0.8])
AC_PREREQ(2.57)
ACJF_INIT

AC_CONFIG_SRCDIR([smoc_root_node.cpp])
AC_CONFIG_AUX_DIR([../BuildSystem])

AC_CANONICAL_BUILD
AC_CANONICAL_HOST

dnl Checks for programs.
ACJF_CHECK_CROSSTOOL(CC, gcc)
ACJF_CHECK_CROSSTOOL(CXX, g++)
ACJF_CHECK_CROSSTOOL(AR, ar)
ACJF_CHECK_CROSSTOOL(RANLIB, ranlib)
ACJF_CHECK_CROSSTOOL(NM, nm)
AC_PROG_CXXCPP
AC_PROG_CPP
# Checks for programs.
#AC_PROG_LIBTOOL
#AC_PROG_INSTALL
#AC_PROG_LN_S
#AC_PROG_MAKE_SET

AC_DISABLE_SHARED
AC_PROG_LIBTOOL

AM_INIT_AUTOMAKE

ACJF_ARG_DEBUG([yes], [], [-DNDEBUG])

AC_CONFIG_HEADER([config.h pkginclude/systemoc/smoc_config.h])

DX_HTML_FEATURE(ON)
DX_CHM_FEATURE(OFF)
DX_CHI_FEATURE(OFF)
DX_MAN_FEATURE(ON)
DX_RTF_FEATURE(OFF)
DX_XML_FEATURE(OFF)
DX_PDF_FEATURE(ON)
DX_PS_FEATURE(OFF)
DX_INIT_DOXYGEN([SysteMoC--mainline], [doxygen.cfg], [doxygen-doc])

dnl check for exe extension
AC_EXEEXT

# Checks for header files.
# AC_FUNC_ALLOCA
# AC_HEADER_STDC
# AC_CHECK_HEADERS([OS.h fcntl.h limits.h mach/mach.h malloc.h stddef.h stdint.h stdlib.h string.h sys/time.h unistd.h])

# Checks for typedefs, structures, and compiler characteristics.

# Checks for library functions.

ACJF_CHECK_LIB_SYSTEMC
ACJF_CHECK_LIB_BOOST
ACJF_CHECK_LIB_XERCES
ACJF_CHECK_LIB_SGX
ACJF_CHECK_LIB_COSUPPORT

### WSDF ###
# --enable-wsdf option
AC_ARG_ENABLE(
 [wsdf],
 [[  --enable-wsdf           Turn on SysteMoC WSDF support [default=yes]]],
 [case "${enableval}" in
    yes)  enable_wsdf=yes  ;;
    no)   enable_wsdf=no   ;;
    auto) enable_wsdf=auto ;;
    *)    AC_MSG_ERROR(bad value ${enableval} for --enable-wsdf) ;;
  esac],
 [enable_wsdf=auto #default]
)

AM_CONDITIONAL([PKG_LIBWSDF_USE_SRCDIR_VERSION], false)
if test x"$enable_wsdf" != x"no"; then
  ACJF_CHECK_LIB_WSDF(
   [enable_wsdf="yes"],
   [if test x"$enable_wsdf" = x"yes"; then
      AC_MSG_ERROR([cannot find LibWSDF package in source tree, bailing out])
    fi
    enable_wsdf="no"])
fi

### SystemC-VPC ###
# --enable-vpc option
AC_ARG_ENABLE(
 [vpc],
 [[  --enable-vpc            enable vpc support for SysteMoC [default=auto]]],
 [case "$enableval" in
    yes)  enable_vpc=yes  ;;
    no)   enable_vpc=no   ;;
    auto) enable_vpc=auto ;;
    *)    AC_MSG_ERROR(bad value ${enableval} for --enable-vpc) ;;
  esac],
 [enable_vpc=auto #default]
)

AM_CONDITIONAL([PKG_SYSTEMC_VPC_USE_SRCDIR_VERSION], false)
if test x"$enable_vpc" != x"no"; then
  ACJF_CHECK_LIB_SYSTEMC_VPC(
   [enable_vpc="yes";],
   [if test x"$enable_vpc" = x"yes"; then
      AC_MSG_ERROR([cannot find SystemC-VPC package in source tree, bailing out])
    fi
    enable_vpc="no"])
fi

AM_CONDITIONAL([SYSTEMOC_ENABLE_VPC], [test x"$pkg_systemc_vpc_srcdir" != "x"])
# This is a define for pkginclude/systemoc/smoc_config.h to guarantee
# consistency for Examples and Testcases
if test x"$pkg_systemc_vpc_srcdir" != "x"; then
  AC_DEFINE([SYSTEMOC_ENABLE_VPC], [yes], [Define if you want SystemC-VPC Support])
fi

AM_CONDITIONAL([SYSTEMOC_ENABLE_WSDF], [test x"$enable_wsdf" = x"yes"])
# This is a define for pkginclude/systemoc/smoc_config.h to guarantee
# consistency for Examples and Testcases
if test x"$enable_wsdf" = x"yes"; then
  AC_DEFINE([SYSTEMOC_ENABLE_WSDF], [yes], [Define if you want WSDF Support])
fi

AC_CONFIG_FILES([
  Makefile
  pkginclude/Makefile
])

ACJF_DONE

AC_OUTPUT
