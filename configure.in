#                                               -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.

AC_INIT([SysteMoC], [0.6], [falk@cs.fau.de], [SysteMoC--mainline--0.6])
AC_PREREQ(2.57)
ACJF_INIT

AC_CONFIG_SRCDIR([smoc_root_node.cpp])
AC_CONFIG_AUX_DIR([../BuildSystem])

AC_CANONICAL_BUILD
AC_CANONICAL_HOST

dnl Checks for programs.
ACJF_CHECK_CROSSTOOL(CC, gcc)
ACJF_CHECK_CROSSTOOL(CXX, g++)
ACJF_CHECK_CROSSTOOL(AR, ar)
ACJF_CHECK_CROSSTOOL(RANLIB, ranlib)
ACJF_CHECK_CROSSTOOL(NM, nm)
AC_PROG_CXXCPP
AC_PROG_CPP
# Checks for programs.
#AC_PROG_LIBTOOL
#AC_PROG_INSTALL
#AC_PROG_LN_S
#AC_PROG_MAKE_SET

AM_INIT_AUTOMAKE

ACJF_ARG_DEBUG([yes], [], [-DNDEBUG])

### SystemC-VPC ###
# --enable-vpc option
AC_ARG_ENABLE(
 [vpc],
 [[  --enable-vpc            enable vpc support for SysteMoC [default=auto]]],
 [case "$enableval" in
    yes)  enable_vpc=yes  ;;
    no)   enable_vpc=no   ;;
    auto) enable_vpc=auto ;;
    *)    AC_MSG_ERROR(bad value ${enableval} for --enable-vpc) ;;
  esac],
 [enable_vpc=auto #default]
)

if test -d $srcdir/wsdf; then
  AC_ARG_ENABLE(
   [wsdf],
   [[  --enable-wsdf           Turn on SysteMoC WSDF support [default=yes]]],
   [case "${enableval}" in
      yes)  enable_wsdf=yes ;;
      no)   enable_wsdf=no  ;;
      *)    AC_MSG_ERROR(bad value ${enableval} for --enable-wsdf) ;;
    esac],
   [enable_wsdf=yes #default]
  )
else
  enable_wsdf=no
fi

AC_CONFIG_HEADER([config.h])

DX_HTML_FEATURE(ON)
DX_CHM_FEATURE(OFF)
DX_CHI_FEATURE(OFF)
DX_MAN_FEATURE(ON)
DX_RTF_FEATURE(OFF)
DX_XML_FEATURE(OFF)
DX_PDF_FEATURE(ON)
DX_PS_FEATURE(OFF)
DX_INIT_DOXYGEN([SysteMoC--mainline], [doxygen.cfg], [doxygen-doc])

AC_DISABLE_SHARED
AM_PROG_LIBTOOL

dnl check for exe extension
AC_EXEEXT

# Checks for header files.
# AC_FUNC_ALLOCA
# AC_HEADER_STDC
# AC_CHECK_HEADERS([OS.h fcntl.h limits.h mach/mach.h malloc.h stddef.h stdint.h stdlib.h string.h sys/time.h unistd.h])

# Checks for typedefs, structures, and compiler characteristics.

# Checks for library functions.

ACJF_CHECK_LIB_SYSTEMC
ACJF_CHECK_LIB_BOOST
ACJF_NEED_PKG([Support])

SYSTEMC_VPC_DEPENDENCIES=""
if test x"$enable_vpc" != x"no"; then
  ACJF_CHECK_PKG([SystemC-VPC], [],
   [test x"$enable_vpc" = x"yes" && {
     AC_MSG_ERROR([cannot find SystemC-VPC package in source tree, bailing out]) } ])
fi
if test x"$pkg_systemc_vpc_srcdir" != "x"; then
  CPPFLAGS="$CPPFLAGS -DENABLE_SYSTEMC_VPC"
  SYSTEMC_VPC_DEPENDENCIES="$pkg_systemc_vpc_builddir/libsystemcvpc.la"
  AM_CPPFLAGS="$AM_CPPFLAGS $SYSTEMC_VPC_INCLUDE"
  AM_LDFLAGS="$AM_LDFLAGS $SYSTEMC_VPC_LDFLAGS"
fi
AC_SUBST([SYSTEMC_VPC_DEPENDENCIES])

AM_CONDITIONAL([WSDF], [test x"$enable_wsdf" = x"yes"])
if test x"$enable_wsdf" = x"yes"; then
  AC_DEFINE([SMOC_ENABLE_WSDF], [yes], [Define if you want WSDF Support])
fi

AC_CONFIG_FILES([Makefile])

ACJF_DONE

AC_OUTPUT
